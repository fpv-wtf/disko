Import('env idir_bin')

taffEnv = env.Clone()

if env['big_lib']:
	Import('libdisko_shared')
	taffEnv['LIBS'] = (['disko']) + taffEnv['LIBS']
elif env['static_lib']:
	Import('libdisko_static')
	taffEnv['LIBS'] = (['disko']) + taffEnv['LIBS']
else:
	Import('libmmsinfo libmmsconfig libmmstools libmmsgui libmmscore libmmsinput libmmsbase')
	if env['media']:
		Import('libmmsmedia')
		taffEnv['LIBS'] = (['mmsmedia', 'mmsinfo', 'mmsconfig', 'mmstools', 'mmsgui', 'mmscore', 'mmsinput', 'mmsbase']) + taffEnv['LIBS']
	else:
		taffEnv['LIBS'] = (['mmsinfo', 'mmsconfig', 'mmstools', 'mmsgui', 'mmscore', 'mmsinput', 'mmsbase']) + taffEnv['LIBS']

taffEnv.Append(LIBPATH = ['../../../lib'])
taff = taffEnv.Program('../../../bin/taff', Glob('src/*.cpp'))

if env['big_lib']:
	Depends(taff, [libdisko_shared])
elif env['static_lib']:
	Depends(taff, [libdisko_static])
else:
	Depends(taff, [libmmsinfo, libmmsconfig, libmmstools, libmmsgui, libmmscore, libmmsinput, libmmsbase])
	if env['media']:
		Depends(taff, [libmmsmedia])

env.Install(idir_bin, taff)
env.Install(idir_bin, env['TOP_DIR'] + '/scripts/create_taffs.sh')
Export('taff')
